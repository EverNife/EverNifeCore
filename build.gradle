buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
}

apply plugin: "com.github.johnrengelman.shadow"

group 'br.com.finalcraft'
version '2.0.3'

targetCompatibility = 1.8
sourceCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javadoc {
    options.encoding = 'UTF-8'
}

//This might keep function(parameter variable's Names) on the compiled jar
compileJava.options.compilerArgs.add '-parameters'
compileTestJava.options.compilerArgs.add '-parameters'

repositories {
    flatDir { //Change this to where are your local libs
        dirs 'C:/Users/Petrus/Desktop/Meus/workshop/workshop/MyOwnPlugins/Bukkit/FinalCraftCore/libs'
        dirs 'C:/Users/Petrus/Desktop/Meus/workshop/workshop/PublicLibs'
        dirs 'C:/Users/Petrus/Desktop/Meus/workshop/workshop/PublicLibs/Bukkit'
    }
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'CodeMC' //NBT API (required for functional-annotation)
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.petrus.dev/public" }
}

dependencies {
    implementation project(':modules:VersionedPlugins')
    implementation project(':modules:v1_7_10')//Also includes 1.12.2 code
    implementation project(':modules:v1_16_5')

    implementation 'com.github.evernife:ChatMenuAPI:v1.1.3c'
    implementation 'com.github.evernife.libby:libby-core:d994023'
    implementation 'dev.triumphteam:triumph-gui:3.0.4-e1'
    implementation 'com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.2'
    implementation 'com.github.EverNife.Item-NBT-API:item-nbt-api:7d30eda0c5'
    implementation 'com.github.EverNife.Item-NBT-API:nbt-data-api:7d30eda0c5'
    implementation 'net.md-5:bungeecord-chat:1.16-R0.1' //This works from 1.7.10 onwards, is the same API

    compileOnly('org.hibernate:hibernate-core:5.4.30.Final')

    compileOnly name: "AuthMe-5.4.0"
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    // @ DISCLAIMER @ //
    // @ DISCLAIMER @ //
    compileOnly name: "EverForgeLib-[1.7.10]"
    // This is a private jar, if you want to compile EverNifeCore you need to remove the folders that use this jar
    // @ DISCLAIMER @ //
    // @ DISCLAIMER @ //
    compileOnly name: "GriefPreventionPlus-13.3"
    compileOnly name: "PlaceholderAPI-2.11.2"

    compileOnly name: "worldguard-bukkit-6.2.2" //Prior to 1.12
    compileOnly name: "worldedit-bukkit-6.0" //Prior to 1.12

    compileOnly name: "Vault-1.6.7"
    compileOnly name: "FeatherBoard-4.28.1" //This is a paid plugin, you need to buy it in order to build this project
    compileOnly name: "BossShopPro-v1.9.8" //This WAS a paid plugin, (you need to buy it in order to build this project)~I Think
    compileOnly name: "MVdWPlaceholderAPI"
    compileOnly name: "ProtocolLib"
    compileOnly name: "craftbukkit1710" //Thats on you to find out
    compileOnly name: "craftbukkit1122" //Thats on you to find out
    compileOnly name: "craftbukkit1165" //Thats on you to find out
    compileOnly group: 'commons-io', name: 'commons-io', version: '2.6' //Necessary for ReflectionUtils

    api 'org.jetbrains:annotations:23.0.0'
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

    //JUnit
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.1")

    //Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

ext.getCallingTask = { ->
    gradle.startParameter.taskNames.size() == 0
            ? ""
            : gradle.startParameter.taskNames.get(0)
}


shadowJar {
    archiveName = "${baseName}-${version}.${extension}"
    dependencies {
        include(dependency('com.github.evernife:ChatMenuAPI:v1.1.3c'))
        include(dependency('com.github.evernife.libby:libby-core:d994023'))
        include(dependency('dev.triumphteam:triumph-gui:3.0.4-e1'))
        include(dependency('com.github.EverNife.Item-NBT-API:item-nbt-api:7d30eda0c5'))
        include(dependency('com.github.EverNife.Item-NBT-API:nbt-data-api:7d30eda0c5'))

        include(project(':modules:VersionedPlugins'))
        include(project(':modules:v1_7_10'))
        include(project(':modules:v1_16_5'))
    }
    if (getCallingTask().equals("publish")){
        println "[Shadowing ExtraJars for Maven Publish]"
        dependencies {
            include(dependency('com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.2'))
        }
        relocate 'org.yaml.snakeyaml', 'br.com.finalcraft.libs.snakeyaml'
    }

    relocate 'de.tr7zw.changeme.nbtapi', 'br.com.finalcraft.libs.nbtapi'
}

java {
 //   withJavadocJar()
 //   withSourcesJar()
}

jar {
    enabled = false //Disable default jar, only shadow jar will be created
    dependsOn(shadowJar { classifier = null })
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "PetrusRepo"
            url = "https://maven.petrus.dev/public"
            credentials {
                username = System.env.PETRUSMAVEN_ACTOR
                password = System.env.PETRUSMAVEN_TOKEN
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        petrus(MavenPublication) {
            groupId = project.group
            from(components.java)
            pom.withXml {
                asNode().appendNode('url','https://petrus.dev')

                asNode().dependencies.'*'.each() {
                    if (it.value().text().contains("triumphteamtriumph")
                    //   || it.value().text().contains("Simple")
                    ){
                        println("Keeping Dependency: " + it.value().text())
                    }else {
                        it.parent().remove(it) //Remove all dependencies from pom file
                    }
                }
                /* this does not work, sad :D
                def repositoriesNode = asNode().appendNode('repositories')
                def repositoryNode = repositoriesNode.appendNode('repository')
                repositoryNode.appendNode('id', 'jitpack.io')
                repositoryNode.appendNode('url', 'https://jitpack.io')
                 */
            }
        }
    }
}